plugins {
    id "com.jfrog.bintray" version "1.1"
    id "com.github.jruby-gradle.base" version "0.1.5"
    id "java"
    id "idea"
}
import com.github.jrubygradle.JRubyExec
repositories {
    mavenCentral()
    jcenter()
}
configurations {
    provided
}

version = "0.1.2"

dependencies {
    compile  "org.embulk:embulk-core:0.6.1"
    compile  "com.hierynomus:sshj:0.11.0"
    provided "org.embulk:embulk-core:0.6.1"
    testCompile "junit:junit:4.+"
}

task classpath(type: Copy, dependsOn: ["jar"]) {
    doFirst { file("classpath").deleteDir() }
    from (configurations.runtime - configurations.provided + files(jar.archivePath))
    into "classpath"
}
clean { delete 'classpath' }

task gem(type: JRubyExec, dependsOn: ["build", "gemspec", "classpath"]) {
    jrubyArgs "-rrubygems/gem_runner", "-eGem::GemRunner.new.run(ARGV)", "build"
    script "build/gemspec"
    doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "pkg") }
}

task gemspec << { file("build/gemspec").write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["Shinichi ISHIMURA"]
  spec.summary       = %[Remote file input plugin for Embulk]
  spec.description   = %[Reads files stored on Remote hosts by SCP.]
  spec.email         = ["shiketaudonko41@gmail.com"]
  spec.licenses      = ["MIT"]
  spec.homepage      = "https://github.com/kamatama41/embulk-input-remote"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]

  spec.add_development_dependency 'bundler', ['~> 1.0']
  spec.add_development_dependency 'rake', ['>= 10.0']
end
/$)
}
